apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'java-gradle-plugin'

repositories {
	maven {
		url 'https://dl.bintray.com/palantir/releases'
		content {
			includeGroupByRegex 'com\\.palantir\\..*'
		}
	}
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.eirslett.frontend-maven-plugin'
		}
	}
	maven {
		url 'http://repository.jsweet.org/artifactory/libs-release-local'
		allowInsecureProtocol true
		content { includeGroup 'org.jsweet' }
	}
	maven { url 'https://plugins.gradle.org/m2/' }
}

Properties props = new Properties()
new File('gradle.properties').withInputStream {
	props.load(it)
}

dependencies {
	// node.js
	api 'com.github.eirslett.frontend-maven-plugin:frontend-plugin-core:ad1062d382' // JitPack HEAD as of 04/23/2020
	// https://github.com/palantir/docker-compose-rule
	api "com.palantir.docker.compose:docker-compose-rule-core:${props.VER_PALANTIR_DOCKER_COMPOSE}"
	api "com.palantir.docker.compose:docker-compose-rule-junit4:${props.VER_PALANTIR_DOCKER_COMPOSE}"
	// https://jdbc.postgresql.org/documentation/changelog.html
	implementation 'org.postgresql:postgresql:42.2.12'
	// jooq codegen
	api 'org.jooq:jooq-codegen:3.13.1'
	// db migration
	api "org.flywaydb:flyway-core:${props.VER_FLYWAY}"
	// jsweet ts2java
	implementation "com.jsoniter:jsoniter:${props.VER_JSONITER}"
	implementation 'org.jsweet:jsweet-transpiler:2.0.1'
	implementation files("${System.properties['java.home']}/../lib/tools.jar")
	// java8 utilities
	implementation 'com.diffplug.durian:durian-core:1.2.0'
}

// if we're not in CI, grab all source
String HEROKU_ENV_VAR = 'STACK'  // https://devcenter.heroku.com/articles/buildpack-api#stacks
String CI_ENV_VAR = 'CI'  // https://circleci.com/docs/1.0/environment-variables/#basics
def keys = System.getenv().keySet()

if (!keys.contains(HEROKU_ENV_VAR) && !keys.contains(CI_ENV_VAR)) {
	// uncomment to generate eclipse project
	// (adds tons of noise to continuous build)
	compileJava.dependsOn(tasks.getByName('eclipse'))
}

eclipse { project { name = 'mytake-buildSrc' } }

gradlePlugin {
	plugins {
		flywayJooq {
			id = 'org.mytake.gradle.flywayjooq'
			implementationClass = 'org.mytake.gradle.flywayjooq.FlywayJooqPlugin'
		}
		node {
			id = 'org.mytake.gradle.node'
			implementationClass = 'org.mytake.gradle.node.NodePlugin'
		}
		jsoniterCodegen {
			id = 'org.mytake.gradle.jsonitercodegen'
			implementationClass = 'org.mytake.gradle.jsonitercodegen.JsoniterCodegenPlugin'
		}
		jsweetRepo {
			id = 'org.mytake.gradle.jsweetrepo'
			implementationClass = 'org.mytake.gradle.jsweet.JsweetRepoPlugin'
		}
	}
}

// reproducible buildSrc jar for travis cache
project.tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
	dirMode = 0775
	fileMode = 0664
}
