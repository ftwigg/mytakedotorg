plugins {
	id 'com.moowork.node' // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
}

////////////////////////////////////////////////////////////
// terrible module system, we copy the common and java2ts //
// directories from the client/src/main/scripts folders   //
// here into the node project                             //
////////////////////////////////////////////////////////////
task syncClientUtils(type: Sync) {
	from '../client/src/main/scripts/common'
	into 'src/main/scripts/common'
}

task syncClientJava2Ts(type: Sync, dependsOn: ':client:jsoniterCodegen') {
	from '../client/src/main/scripts/java2ts'
	into 'src/main/scripts/java2ts'
}

task beforeCompile(dependsOn: [
	syncClientUtils,
	syncClientJava2Ts
])

spotless {
	format 'typescript', {
		target fileTree('src/main/scripts') {
			include '**'
			exclude 'java2ts/**'
		}
		prettier().config(['parser': 'typescript'])
	}
	format 'js', {
		target 'gulpfile.js'
		prettier().config(['parser': 'typescript'])
	}
}

/////////////////////////////////
// smoke test the node project //
/////////////////////////////////
node {
	// node 10.x is needed for node-canvas
	version = '10.11.0'
	npmVersion = '6.4.1'
	download = true
}

npm_run_test.dependsOn(beforeCompile)
task check(dependsOn: npm_run_test)
