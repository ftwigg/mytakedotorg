////////////////////////////////////////////////////////////
// terrible module system, we copy the common and java2ts //
// directories from the client/src/main/scripts folders   //
// here into the node project                             //
////////////////////////////////////////////////////////////
tasks.register('syncClientUtils', Sync) {
	from '../client/src/main/scripts/common'
	into 'src/main/scripts/common'
}

tasks.register('syncClientJava2Ts', Sync) {
	dependsOn ':client:jsoniterCodegen'
	from '../client/src/main/scripts/java2ts'
	into 'src/main/scripts/java2ts'
}

tasks.register('beforeCompile') {
	dependsOn syncClientUtils, syncClientJava2Ts
}

apply plugin: 'com.diffplug.gradle.spotless'
spotless {
	format 'typescript', {
		target fileTree('src/main/scripts') {
			include '**/*.ts'
			exclude 'java2ts/**'
		}
		prettier().config(['parser': 'typescript'])
	}
	format 'js', {
		target 'gulpfile.js'
		prettier().config(['parser': 'typescript'])
	}
}

/////////////////////////////////
// smoke test the node project //
/////////////////////////////////
apply plugin: 'org.mytake.gradle.node'
node {
	// node 10.x is needed for node-canvas
	setup.nodeVersion = 'v10.11.0'
	setup.npmVersion = 'provided'

	tasks.check.dependsOn npmRun('test') {
		dependsOn 'beforeCompile'
		inputs.dir('src/main/scripts')
	}
}
