plugins {
	id 'com.moowork.node' version '1.2.0' // https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
	id 'com.moowork.gulp' version '1.2.0' // https://github.com/srs/gradle-node-plugin/blob/master/docs/gulp.md
	id 'de.richsource.gradle.plugins.typescript' version '1.8.0' // https://github.com/sothmann/typescript-gradle-plugin#configuring-the-typescript-compile-task
}

import com.moowork.gradle.node.NodeExtension
import com.moowork.gradle.node.variant.VariantBuilder

node {
	version = '6.10.2'
	npmVersion = '5.2.0'
	// yarnVersion = '0.16.1'
	download = true
}

String nodeExecutable() {
	NodeExtension nodeExt = NodeExtension.get(project)
	return new VariantBuilder(nodeExt).build().nodeExec
}

compileTypeScript {
	compilerExecutable "${nodeExecutable()} node_modules/typescript/lib/tsc.js"
	dependsOn "npmInstall"

	projectFile = file('tsconfig.json')
	source = file('src')
}

// make the gulp_buildProd have accurate up-to-date checking
gulp_buildProd.dependsOn 'npmInstall'
gulp_buildProd.inputs.dir('assets')
gulp_buildProd.inputs.dir('loaders')
gulp_buildProd.inputs.dir('nunjucks')
gulp_buildProd.inputs.dir('src')
gulp_buildProd.inputs.file('gulpfile.js')
gulp_buildProd.inputs.file('package.json')
gulp_buildProd.inputs.file('postcss.config.js')
gulp_buildProd.inputs.file('tsconfig.json')
gulp_buildProd.inputs.file('webpack.config.js')
gulp_buildProd.outputs.dir('dist')

// map prettier to spotless (to match server)
task spotlessCheck(dependsOn: npm_run_formatlist)
task spotlessApply(dependsOn: npm_run_formatwrite)

// check should run all tests
task check(dependsOn: [
	npm_run_test,
	spotlessCheck
])
